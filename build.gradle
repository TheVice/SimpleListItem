/*
 * This build file was auto generated by running the Gradle 'init' task
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle user guide
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'

/*
// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}
*/
//sourceCompability = 1.6 // sets Java version compilation compatibility to 1.6

jar {
	manifest {
		attributes 'Main-Class': 'Main' // adds Main-Class header to JAR file's manifest
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}

task printVersion {
	doLast {
		println "Version: $version"
	}
}

ext.versionFile = file('version.properties') // identifies project's version

task loadVersion {
	project.version = readVersion()
}

ProjectVersion readVersion() {
	logger.quiet 'Reading the version file.'

	if(!versionFile.exists()) {
		throw new GradleException("Required version file does not exists: $versionFile.canonicalPath")
	}

	Properties versionProps = new Properties()

	versionFile.withInputStream {
		stream->versionProps.load(stream)
	}

	new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger())
}

task createDistribution(type: Zip, dependsOn: build) {
	from jar.outputs.files

	from(sourceSets*.allSource) {
		into 'src'
	}

	from(rootDir) {
		include versionFile.name
	}
}

tasks.addRule("Pattern: increment<Classifier>Version - Increments the project version classifier.") {
	String taskName -> if(taskName.startsWith('increment') && taskName.endsWith('Version')) {
		task(taskName) << {
			String classifier = (taskName - 'increment' - 'Version').toLowerCase()
			String currentVersion = version.toString()

			switch(classifier) {
				case 'major': ++version.major
					break
				case 'minor': ++version.minor
					break
				default: throw new GradleException("Invalid version type '$classifier'. Allowed types: ['Major', 'Minor']")
			}

			String newVersion = version.toString()
			logger.info "Incrementing $classifier project version: $currentVersion -> $newVersion"

			ant.propertyfile(file: versionFile) {
				entry(key: classifier, type: 'int', operation: '+', value: 1)
			}
		}
	}
}
